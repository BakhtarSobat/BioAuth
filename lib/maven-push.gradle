/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def isReleaseBuild() {
    return libraryVersion.contains("SNAPSHOT") == false
}

ext{
    releaseRepositoryUrl=properties.getProperty("nexus.repo.release.url")
    snapshotRepositoryUrl=properties.getProperty("nexus.repo.snapshots.url")
    repositoryUsername= properties.getProperty("nexus.user")
    repositoryPassword=properties.getProperty("nexus.password")
}


afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {


                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = publishedGroupId
                pom.artifactId = artifact
                pom.version = libraryVersion

                repository(url: releaseRepositoryUrl) {
                    authentication(userName: repositoryUsername, password: repositoryPassword)
                }
                snapshotRepository(url: snapshotRepositoryUrl) {
                    authentication(userName: repositoryUsername, password: repositoryPassword)
                }

                pom.project {
                    name artifact
                    packaging packing
                    description libraryDescription
                    url siteUrl

                    scm {
                        url gitUrl
                        connection gitUrl
                        developerConnection gitUrl
                    }

                    licenses {
                        license {
                            name licenseName
                            url licenseUrl
                            distribution allLicenses
                        }
                    }

                    developers {
                        developer {
                            id developerId
                            name developerName
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    //task androidJavadocs(type: Javadoc) {
    //source = android.sourceSets.main.allJava
    //}

    //task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    //classifier = 'javadoc'
    //from androidJavadocs.destinationDir
    //}

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
    }
}